plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "java"
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = "cc.rabbitcontrol"
version = "1.0-SNAPSHOT"


dependencies {
    implementation ("cc.rabbitcontrol:rcp:0.3.39")

    testImplementation ('junit:junit:4.13.2')
}

task runServer(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    mainClass = "cc.rabbitcontrol.RCPServerTest"
    if(System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

jar {
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                //'Build-Revision' : versioning.info.commit,
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Main-Class'      : "io.github.rabbitcontrol.RCPServerTest"
        )
    }
}
